#+ method: GET
#+ summary: Query that returns a list of the available configurations, inputs and outputs given a certain model ?_model
#+ defaults:
#+   - model: https://w3id.org/okn/i/mint/PIHM

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sdm: <https://w3id.org/okn/o/sdm#>
prefix sd: <https://w3id.org/okn/o/sd#>

SELECT ?version ?config ?configDesc ?compLoc (GROUP_CONCAT(DISTINCT ?input;SEPARATOR=', ') AS ?input_files) (GROUP_CONCAT(DISTINCT ?output;SEPARATOR=', ') AS ?output_files)
WHERE {
  ?_model_iri a sdm:Model.
  ?_model_iri sd:hasVersion ?version.
  ?version sd:hasConfiguration ?config.
  OPTIONAL{?config sd:hasComponentLocation ?compLoc}.
  OPTIONAL{?config sd:description ?configDesc.}.
  OPTIONAL{?config sd:hasInput ?input.}.
  OPTIONAL{?config sd:hasOutput ?output.}.
}group by ?version ?config ?configDesc ?compLoc