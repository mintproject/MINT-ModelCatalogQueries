#+ method: GET
#+ summary: Query that returns a list of the available configurations, input variables and output variables given a certain model ?_model
#+ defaults:
#+   - model: https://w3id.org/mint/instance#TOPOFLOW

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mc: <https://w3id.org/mint/modelCatalog#>
PREFIX os: <http://ontosoft.org/software#>
PREFIX dc: <http://purl.org/dc/terms/>

SELECT ?version ?config ?configDesc ?compLoc (GROUP_CONCAT(DISTINCT ?input;SEPARATOR=', ') AS ?input_files) (GROUP_CONCAT(DISTINCT ?output;SEPARATOR=', ') AS ?output_files)
WHERE {
  ?_model_iri a mc:Model.
  ?_model_iri os:hasSoftwareVersion ?version.
  ?version mc:hasConfiguration ?config.
  OPTIONAL{?config mc:hasComponentLocation ?compLoc}.
  OPTIONAL{?config dc:description ?configDesc.}.
  OPTIONAL{?config mc:hasInput ?input.}.
  OPTIONAL{?config mc:hasOutput ?output.}.
}group by ?version ?config ?configDesc ?compLoc