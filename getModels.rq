#+ method: GET

#+ summary: Query that returns a label, description, documentation, assumptions, restrictions, category and versions of each model

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sdm: <https://w3id.org/okn/o/sdm#>
prefix sd: <https://w3id.org/okn/o/sd#>

SELECT ?model ?label ?desc ?doc ?dateC ?dateP ?fundS ?contactP ?downloadURL ?faq ?installInstr ?purpose ?keywords ?logo ?memReq ?os ?procReq ?publisher ?referenceP ?shortDesc ?eq ?grid ?modelType ?sourceC ?pl
(GROUP_CONCAT(DISTINCT ?as;SEPARATOR=', ') AS ?assumptions) (GROUP_CONCAT(DISTINCT ?r;SEPARATOR=', ') AS ?restrictions) (GROUP_CONCAT(DISTINCT ?cat;SEPARATOR=', ') AS ?categories) (GROUP_CONCAT(DISTINCT ?v;SEPARATOR=', ') AS ?versions)
(GROUP_CONCAT(DISTINCT ?auth;SEPARATOR=', ') AS ?authors)
(GROUP_CONCAT(DISTINCT ?cit;SEPARATOR=', ') AS ?citations)
(GROUP_CONCAT(DISTINCT ?contrib;SEPARATOR=', ') AS ?contributors)
(GROUP_CONCAT(DISTINCT ?sampleVis;SEPARATOR=', ') AS ?sampleVisualization)
(GROUP_CONCAT(DISTINCT ?typicalD;SEPARATOR=', ') AS ?typicalDataSource)
(GROUP_CONCAT(DISTINCT ?scr;SEPARATOR=', ') AS ?screenshots)
(GROUP_CONCAT(DISTINCT ?explD;SEPARATOR=', ') AS ?explanationDiagrams)
from <http://ontosoft.isi.edu:3030/modelCatalog-1.0.0/data/mint> 
WHERE {
  ?model a sdm:Model.
  ?model rdfs:label ?label.
  ?model a ?modelType.
  FILTER(?modelType != sdm:Model)
  OPTIONAL{?model sd:hasAssumption ?as}.
  OPTIONAL{?model sd:hasConstraint ?r}.
  OPTIONAL{?model sd:description ?desc}.
  OPTIONAL{?model sdm:hasModelCategory ?cat}.
  OPTIONAL{?model sd:hasVersion ?v}.
  OPTIONAL{?model sd:hasDocumentation ?doc}.
  OPTIONAL{?model sd:author ?a.
  		   ?a rdfs:label ?auth}.
  OPTIONAL{?model sd:citation ?cit}.
  OPTIONAL{?model sd:contributor ?contrib}.
  OPTIONAL{?model sd:dateCreated ?dateC}.
  OPTIONAL{?model sd:datePublished ?dateP}.
  OPTIONAL{?model sd:fundingSource	 ?fund.
           ?fund rdfs:label ?fundS}.
  OPTIONAL{?model sd:hasContactPerson ?contact. 
            ?contact rdfs:label ?contactP}.
  OPTIONAL{?model sd:hasDownloadURL ?downloadURL}.
  OPTIONAL{?model sd:hasFAQ ?faq}.
  OPTIONAL{?model sd:hasInstallationInstructions ?installInstr}.
  OPTIONAL{?model sd:hasPurpose ?purpose}.
  OPTIONAL{?model sd:hasSampleVisualization ?sample. ?sample <http://www.w3.org/ns/prov#value> ?sampleVis}.
  OPTIONAL{?model sd:hasSourceCode ?s.
           ?s sd:programmingLanguage ?pl.
           ?s sd:codeRepository ?sourceC}.
  OPTIONAL{?model sd:hasTypicalDataSource ?typicalD}.
  OPTIONAL{?model sd:keywords ?keywords}.
  OPTIONAL{?model sd:logo ?l. 
  		   ?l <http://www.w3.org/ns/prov#value> ?logo}.
  OPTIONAL{?model sd:memoryRequirements ?memReq}.
  OPTIONAL{?model sd:operatingSystems ?os}.
  OPTIONAL{?model sd:processorRequirements ?procReq}.
  OPTIONAL{?model sd:publisher ?pub.
           ?pub rdfs:label ?publisher}.
  OPTIONAL{?model sd:referencePublication ?referenceP}.
  OPTIONAL{?model sd:screenshot ?scr}.
  OPTIONAL{?model sd:shortDescription ?shortDesc}.
  OPTIONAL{?model sd:softwareRequirements ?softwareReq}.
  OPTIONAL{?model sd:website ?web}.
  OPTIONAL{?model sdm:hasEquation ?eq}.
  OPTIONAL{?model sdm:hasGrid ?grid}.
  OPTIONAL{?model sdm:hasExplanationDiagram ?explD}.
    
}group by ?model ?label ?desc ?doc ?dateC ?dateP ?fundS ?contactP ?downloadURL ?faq ?installInstr ?purpose ?keywords ?logo ?memReq ?os ?procReq ?publisher ?referenceP ?shortDesc ?eq ?grid ?modelType ?pl ?sourceC
