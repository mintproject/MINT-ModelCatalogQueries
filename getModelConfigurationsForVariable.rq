#+ method: GET
#+ summary: Query that given a standard variable, returns the model configurations which produce output variables with that standard name.
#+ defaults:
#+   - std: land_surface__albedo

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mc: <https://w3id.org/mint/modelCatalog#>
PREFIX os: <http://ontosoft.org/software#>
PREFIX dc: <http://purl.org/dc/terms/>

SELECT DISTINCT ?model ?desc ?version ?configuration ?compLoc
WHERE {
  ?model a mc:Model.
  ?model os:hasSoftwareVersion ?version.
  OPTIONAL{?model dc:description ?desc}.
  ?version mc:hasConfiguration ?configuration.
  OPTIONAL{?configuration mc:hasComponentLocation ?compLoc}.
  ?configuration mc:hasOutput ?output.
  ?output mc:hasPresentation ?vp.
  ?vp mc:hasStandardVariable ?sv.
  ?sv rdfs:label ?_std_string.
}