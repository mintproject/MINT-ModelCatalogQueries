#+ method: GET
#+ summary: Given a standard name (?_std_string), this query returns which input (or output) and its type, configuration, version and model refers to it.
#+ defaults:
#+   - std: soil__residual_porosity

prefix sdm: <https://w3id.org/okn/o/sdm#>
prefix sd: <https://w3id.org/okn/o/sd#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?io ?type ?model_config ?compLoc ?modelVersion ?model
WHERE {
  ?io a sd:DatasetSpecification.
  ?io a ?type.
  FILTER(?type != sd:DatasetSpecification).
  ?io sd:hasPresentation ?presentation.
  ?presentation sd:hasStandardVariable ?sv.
  ?sv rdfs:label ?_std_string.
  ?model sd:hasVersion ?modelVersion.
  ?modelVersion sd:hasConfiguration ?model_config.
  OPTIONAL{?model_config sd:hasComponentLocation ?compLoc}.
  {  
  	?model_config sd:hasInput ?io.
  }
  UNION
  {
    ?model_config sd:hasOutput ?io.
  }
}