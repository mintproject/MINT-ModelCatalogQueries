#+ method: GET
#+ summary: Query that returns returns the variablePresentations associated to the provided input/output, and their units. Additionally, this query returns the variable presentation label, description, long name, short name and standard variable.
#+ defaults:
#+   - io: https://w3id.org/mint/instance/pihm_forc

PREFIX mc: <https://w3id.org/mint/modelCatalog#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT distinct ?vp ?label ?shortName ?longName ?description ?unit ?sn ?rl
WHERE {
  ?_io_iri mc:hasPresentation ?vp.
  OPTIONAL{
    ?vp mc:usesUnit ?u.
    ?u rdfs:label ?unit.
  }.
  ?vp rdfs:label ?label.
  OPTIONAL{?vp dc:description ?description.}.
  OPTIONAL{?vp mc:hasLongName ?longName.}.
  OPTIONAL{?vp mc:hasShortName ?shortName.}.
  OPTIONAL{?vp mc:hasStandardVariable ?svn. ?svn rdfs:label ?sn}.
  OPTIONAL{?vp mc:hasRelevanceLevel ?rl.}.
}